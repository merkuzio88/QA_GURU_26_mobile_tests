plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
}

group = 'com.merkuzio'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def restAssuredVersion = "5.4.0",
    allureVersion = "2.21.0",
    selenideVersion = "6.19.1",
        ownerVersion = "1.0.12"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.rest-assured:json-schema-validator:$restAssuredVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "io.appium:java-client:8.6.0",
            "org.junit.jupiter:junit-jupiter:5.10.2",
            "org.slf4j:slf4j-simple:2.0.12",
            "org.assertj:assertj-core:3.6.1"
    )
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task ios_tests(type: Test) {
    useJUnitPlatform {
        includeTags("ios_tests")
    }
}
task android_tests(type: Test) {
    useJUnitPlatform {
        includeTags("android_tests")
    }
}

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}